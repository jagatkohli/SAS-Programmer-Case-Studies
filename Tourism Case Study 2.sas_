/*Tourism Case Study 2*/

/*Create the Cleaned_Tourism Table*/
data cleaned_tourism;
	length Country_Name $300 Tourism_Type $20;
	retain Country_Name "" Tourism_Type "";
	set cr.tourism(drop=_1995-_2013);
	if A ne . then Country_Name=Country;
	if lowcase(Country) = "inbound tourism" then Tourism_Type = "Inbound tourism";
		else if lowcase(Country) ='outbound tourism' then Tourism_Type = "Outbound tourism";
	if Country_Name ne Country and Country ne Tourism_Type;
	series = upcase(series);
	if series = ".." then Series="";
	ConversionType=scan(country,-1," ");
	if _2014 =".." then _2014 = ".";
	if ConversionType = 'Mn' then do;
		if _2014 ne "." then Y2014 = input(_2014,16.) * 1000000;
			else Y2014=.;
		Category=cat(scan(country,1,'-','r'),' - US$');
	end;
	else if ConversionType = "Thousands" then do;
		if _2014 ne "." then Y2014 = input(_2014,16.) * 1000;
			else Y2014=.;
		Category=scan(country,1,'-','r');
	end;
	format y2014 comma25.;
	drop A ConversionType Country _2014;
run;



proc freq data=cleaned_tourism;
	tables country category;
run;

proc freq data=cleaned_tourism;
	tables Category Tourism_Type Series;
run;

proc means data = cleaned_tourism min mean max n maxdec=0;
	var Y2014;
run;

/*Creating Final_Tourism Table*/
proc format;
	value contIDs
		1 = "North America"
		2 = "South America"
		3 = "Europe"
		4 = "Africa"
		5 = "Asia"
		6 = "Oceana"
		7 = "Antarctica";
run;

proc sort data = cr.country_info(rename=(Country=Country_Name))
		out=country_sorted;
	by country_name;
run;

data final_tourism;
	merge cleaned_tourism(in=t) Country_Sorted(in=c);
	by country_name;
	if t=1 and c=1 then output Final_Tourism;
	format continent contIDs.;
run;

proc freq data = final_tourism nlevels;
	tables category series Tourism_Type Continent / nocum nopercent;
run;

*Q1 answer;
proc means data=final_tourism min mean max maxdec=0;
	var Y2014;
	class Continent;
	where Category = "Arrivals";
run;

*Q2 answer;
proc means data=final_tourism mean maxdec=0;	
	var y2014;
	where lowcase(Category) contains "tourism expenditure in other countries";
run;